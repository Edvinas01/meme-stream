apply plugin: 'kotlin'

version '0.1'
group 'doggo-bot'

buildscript {
    ext {
        kotlinLoggingVersion = '1.4.6'
        twitter4jVersion = '4.0.6'
        jacksonVersion = '2.9.2'
        detektVersion = '1.0.0.RC5-3'
        kotlinVersion = '1.1.51'
        slf4jVersion = '1.7.25'
        mapDbVersion = '3.0.5'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    detekt
}

dependencies {

    // Core dependencies.
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "org.twitter4j:twitter4j-core:$twitter4jVersion"

    // Storage.
    compile "org.mapdb:mapdb:$mapDbVersion"

    // JSON and Yaml parsing dependencies.
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"

    // Logging dependencies.
    compile "io.github.microutils:kotlin-logging:$kotlinLoggingVersion"
    compile "org.slf4j:slf4j-log4j12:$slf4jVersion"
    compile "org.slf4j:slf4j-api:$slf4jVersion"

    // Tests and stuff.
    detekt "io.gitlab.arturbosch.detekt:detekt-cli:$detektVersion"
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}
compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}

task detekt(type: JavaExec) {
    classpath = configurations.detekt
    main = 'io.gitlab.arturbosch.detekt.cli.Main'

    def params = [
            '-i', "$projectDir",
            '-c', "$projectDir/.detekt.yml",
            '-f', '.*test.*',
    ]

    args(params)
}

clean.doFirst {
    delete "${rootDir}/out"
}